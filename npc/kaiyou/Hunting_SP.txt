// Daily Hunting Missions
// ==============================================================================

prontera,164,165,1	script	Busca Empleo?::MissionSign	837,{
	mes "^0000FF* Busca empleo?";
	mes "* Tiene habilidades para cazar criaturas?";
	mes "* Quiere ganar artículos y dinero?^000000";
	mes "...";
	if( BaseLevel < 50 )
	{
		mes "Sólo falta que te hagas más fuerte!";
		close;
	}
	mes "Busque a Issan en la sala derecha donde se encuentra el refine de prontera y no pierda más tiempo!";

	set .@Time_Left, Mission_Tick - gettimetick(2);

	if( (Mission_Ended == 0 || .@Time_Left < 0 ) && BaseLevel > 49 )
		navigateto "prt_in",90,72;
	close;
	end;

OnPCLoadMapEvent:
	set .@Time_Left, Mission_Tick - gettimetick(2);
	
	if(Mission_ID1 != 0 && .@Time_Left > 0 || BaseLevel < 50)
		end;
	else
		if( .@Time_Left > 0 )
			end;
		else
			showevent QTYPE_QUEST,0;
	end;

OnWhisperGlobal:
	if ( getgmlevel() < 60) 
		end;
	set .@i,1;
OnClock0030:
	set .@rand, rand(0,33);

	if(.@i)
		input .@rand;

	switch(.@rand)
	{
		default:
			//Amatsu
			setarray .@mob[0],1031,1060,1068,1400,1402,1404,1406,1401,1403,1129,1375,1191,1405;
			setarray .@mobMvP[0], 1492;
			set $WarperFree,1;
			break;
		case 1:
			//Ayothaya
			setarray .@mob[0],1036,1179,1587,1584,1081,1494,1056,1057,1080,1104,1586,1499;
			setarray .@mobMvP[0], 1688;
			set $WarperFree,2;
			break;
		case 2:
			//Comodo
			setarray .@mob[0],1271,1067,1073,1074,1254,1391,1206,1256,1266,1323,1119,1258,1282,1037,1165,1215,1009,1030,1042,1148,1255,1274,1278,1279,1034,1064;
			setarray .@mobMvP[0], 1583;
			set $WarperFree,3;
			break;
		case 3:
			//Einbroch
			setarray .@mob[0],1199,1378,1380,1613,1628,1121,1368,1614,1615,1627,1619,1005,1145,1175,1111,1169,1151,1616,1617,1620,1622;
			setarray .@mobMvP[0], 1623;
			set $WarperFree,4;
			break;
		case 4:
			//Geffen
			setarray .@mob[0],1002,1007,1008,1012,1023,1213,1261,1134,1040,1094,1273,1258,1392,1282,1189,1035,1077,1015,1036,1061,1111,1130,1109,1143;
			setarray .@mobMvP[0], 1087,1190,1389,1046;
			set $WarperFree,5;
			break;
		case 5:
			//Gonryun
			setarray .@mob[0],1409,1415,1417,1408,1410,1412,1416,1413,1081,1031,1080,1082,1078;
			setarray .@mobMvP[0], 1418;
			set $WarperFree,6;
			break;
		case 6:
			//Hugel
			setarray .@mob[0],1368,1378,1627,1715,1718,1155,1384,1156,1385,1692,1687,1628,1721,1191,1216,1699,1714,1717,1713,1716;
			setarray .@mobMvP[0], 1719;
			set $WarperFree,7;
			break;
		case 7:
			//Lighthalzen
			setarray .@mob[0],1627,1652,1653,1654,1655,1656,1657,1682,1634,1635,1636,1637,1638,1639,1613,1628,1103,1215,1680;
			setarray .@mobMvP[0], 1658;
			set $WarperFree,8;
			break;
		case 8:
			//Louyang
			setarray .@mob[0],1037,1060,1139,1516,1129,1381,1517,1026,1512,1514,1513,1631;
			setarray .@mobMvP[0], 1630;
			set $WarperFree,9;
			break;
		case 9:
			//Lutie
			setarray .@mob[0],1002,1113,1242,1243,1031,1246,1265,1248,1249;
			setarray .@mobMvP[0], 1251,1252;
			set $WarperFree,10;
			break;
		case 10:
			//Morocc
			setarray .@mob[0],1047,1097,1107,1113,1019,1010,1031,1033,1116,1277,1049,1050,1040,1055,1095,1105,1127,1138,1160,1025,1030,1037,1106,1165,1119,1058,1005,1176;
			setarray .@mobMvP[0], 1159,1147;
			set $WarperFree,11;
			break;
		case 11:
			//Moscovia
			setarray .@mob[0],1031,1880,1881,1882,1883,1884,1081,1082,1060,1103,1139;
			setarray .@mobMvP[0], 1885;
			set $WarperFree,12;
			break;
		case 12:
			//Mjolnir
			setarray .@mob[0],1018,1079,1080,1103,1104,1128,1174,1004,1031,1118,1155,1100,1139,1156,1099,1077,1094,1013,1020,1054,1053,1114,1215;
			setarray .@mobMvP[0], 1059;
			set $WarperFree,13;
			break;
		case 13:
			//Niflheim
			setarray .@mob[0],1503,1504,1506,1508,1510,1505,1507,1509;
			setarray .@mobMvP[0], 1373;
			set $WarperFree,14;
			break;
		case 14:
			//Payon
			setarray .@mob[0],1002,1010,1014,1031,1013,1025,1008,1063,1113,1242,1077,1024,1053,1033,1060,1116,1056,1110,1277,1321,1005,1015,1076,1016,1028,1111,1068,1188,1026,1170;
			setarray .@mobMvP[0], 1115,1150;
			set $WarperFree,15;
			break;
		case 15:
			//Prontera
			setarray .@mob[0],1002,1004,1007,1008,1018,1063,1051,1020,1056,1057,1052,1048,1009,1047,1049,1107,1167,1166,1258,1005,1175,1014,1053,1161,1054,1077,1209;
			setarray .@mobMvP[0], 1086;
			set $WarperFree,16;
			break;
		case 16:
			//Rachel
			setarray .@mob[0],1368,1780,1781,1783,1782,1784,1261,1680,1627,1113,1776,1778,1775,1777,1789;
			setarray .@mobMvP[0], 1785,1779;
			set $WarperFree,17;
			break;
		case 17:
			//Umbala / Laberynth
			setarray .@mob[0],1114,1215,1376,1493,1499,1104,1118,1214,1494,1495,1321,1500,1271,1128,1380,1035,1037,1101,1139;
			setarray .@mobMvP[0], 1039;
			set $WarperFree,18;
			break;
		case 18:
			//Veins
			setarray .@mob[0],1037,1055,1113,1784,1166,1782,1165,1836,1106,1780,1386,1781,1254;
			setarray .@mobMvP[0], 1785;
			set $WarperFree,19;
			break;
		case 19:
			//Yuno
			setarray .@mob[0],1002,1031,1114,1369,1035,1128,1494,1113,1242,1368,1162,1378,1386,1372,1376,1380;
			setarray .@mobMvP[0], 1388;
			set $WarperFree,20;
			break;
		case 20:
			//Byaland / Sunked
			setarray .@mob[0],1066,1068,1070,1141,1161,1067,1044,1144,1045,1142,1264,1065,1108,1071,1077,1179,1216;
			setarray .@mobMvP[0], 1112;
			set $WarperFree,21;
			break;
		case 21:
			//Clock Tower / Glast Heim
			setarray .@mob[0],1102,1195,1199,1269,1193,1191,1320,1194,1189,1211,1213,1209,1216,1111,1131,1253,1036,1117,1015,1192,1260,1267,1281,1163,1208,1201,1197,1257,1202,1276,1219,1132,1143,1263,1207,1206;
			setarray .@mobMvP[0], 1272;
			set $WarperFree,22;
			break;
		case 22:
			//Juperos
			setarray .@mob[0],1670,1671,1672,1673,1676,1677,1678,1679,1365,1668;
			setarray .@mobMvP[0], 1685;
			set $WarperFree,23;
			break;
		case 23:
			//Robot Factory
			setarray .@mob[0],1275,1735,1736,1737,1738,1745;
			setarray .@mobMvP[0], 1734;
			set $WarperFree,24;
			break;
		case 24:
			//Magma
			setarray .@mob[0],1072,1366,1367,1381,1383,1379,1382,1384,1385,1387;
			setarray .@mobMvP[0], 1582,1096;
			set $WarperFree,25;
			break;
		case 25:
			//Nameless
			setarray .@mob[0],1036,1864,1865,1866,1867,1869;
			setarray .@mobMvP[0], 1871,1874;
			set $WarperFree,26;
			break;
		case 26:
			//Odin Temple
			setarray .@mob[0],1692,1752,1753,1754,1755,1693;
			setarray .@mobMvP[0], 1751,1765;
			set $WarperFree,27;
			break;
		case 27:
			//Orc Dungeon
			setarray .@mob[0],1023,1273,1686,1005,1042,1152,1153,1111,1177;
			setarray .@mobMvP[0], 1190;
			set $WarperFree,28;
			break;
		case 28:
			//Pyramid
			setarray .@mob[0],1005,1031,1016,1028,1041,1111,1032,1029,1146,1191,1297,1149,1194;
			setarray .@mobMvP[0], 1038,1511;
			set $WarperFree,29;
			break;
		case 29:
			//Rachel Sanctuary
			setarray .@mob[0],1632,1633,1771,1772,1773,1774,1769,1770;
			setarray .@mobMvP[0], 1768;
			set $WarperFree,30;
			break;
		case 30:
			//Sphinx
			setarray .@mob[0],1111,1146,1164,1178,1140,1154,1191,1098,1037;
			setarray .@mobMvP[0], 1157;
			set $WarperFree,31;
			break;
		case 31:
			//Thanatos
			setarray .@mob[0],1191,1195,1275,1695,1703,1377,1697,1698,1699,1702,1701,1295,1320,1694,1700;
			setarray .@mobMvP[0], 1708;
			set $WarperFree,32;
			break;
		case 32:
			//Thor
			setarray .@mob[0],1831,1833,1836,1837,1072,1838;
			setarray .@mobMvP[0], 1832;
			set $WarperFree,33;
			break;
		case 33:
			//Turtle Island
			setarray .@mob[0],1034,1256,1314,1321,1322,1316,1319,1315,1318;
			setarray .@mobMvP[0], 1312;
			set $WarperFree,34;
			break;
	}
	
	for( set .@i, 1; .@i < 5; set .@i, .@i + 1 )
	{
		do{
			set .@repetido , 0;
			.@randmob = rand(0,getarraysize(.@mob));
			for( set .@j, 1; .@j < .@i; set .@j, .@j + 1 )
			{
				set .@mobidrepetido, getd("$Hunting" + .@j);
				if(  .@mobidrepetido == .@mob[.@randmob] )
					set .@repetido,1;
			}
		}
			while ( .@mob[.@randmob] == 0 || .@repetido == 1);
		setd("$Hunting"+.@i),.@mob[.@randmob];
		setd("$HuntingQ"+.@i),20 + rand(5,35);
	}
	do{
		.@randmob = rand(0,getarraysize(.@mobMvP));
	}
		while ( .@mobMvP[.@randmob] == 0 );
	
	set $Hunting5,.@mobMvP[.@randmob];
	set $HuntingQ5,1;
	end;
}

prontera	mapflag	loadevent
prt_in	mapflag	loadevent

function	script	MissionInfo	{
	set .@Mission_Lvl, 0;
	set .@Mission_Exp, 0;
	set .@Mission_Job, 0;
	
	mes "[^2d8bd2Contrato-Misión^000000]";
	for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
	{
		set .@MobID, getd("Mission_ID" + .@i);
		mes "^0000FF" + getd("Mission_Count" + .@i) + "^000000 x ^2d8bd2" + strmobinfo(2,.@MobID) + "^000000 (ID : " + .@MobID + ")";

		set .@Mission_Lvl, .@Mission_Lvl + strmobinfo(3, .@MobID);
		set .@Mission_Exp, .@Mission_Exp + (strmobinfo(6, .@MobID) * 8);
		set .@Mission_Job, .@Mission_Job + (strmobinfo(7, .@MobID) * 8);
	}

	set .@Mission_Zeny, .@Mission_Lvl * 500;
	if (.@Mission_Zeny > 250000)
		.@Mission_Zeny = 250000;
	next;
	
	set .@Mission_Lvl, .@Mission_Lvl / 4;
	
	mes "[^2d8bd2Detalles-Misión^000000]";
	mes "Nivel : ^0000FF" + .@Mission_Lvl + "^000000";

	if( .@Mission_Lvl < 50 )
	{
		mes "Dificultad : ^008000Bajo^000000";
		set .@Mission_Points, 1;
	}
	else if( .@Mission_Lvl < 90 )
	{
		mes "Dificultad : ^000080Media^000000";
		set .@Mission_Points, 2;
	}
	else
	{
		mes "Dificultad : ^FF0000Alta^000000";
		set .@Mission_Points, 4;
	}
	
	set .@Time_Left, Mission_Tick - gettimetick(2);
	if( .@Time_Left <= 0 )
		mes "Tiempo Restante : ^FF0000Expirado^000000";
	else if( .@Time_Left > 3600 )
		mes "Tiempo Restante : ^0000FF" + (.@Time_Left / 3600) + " hora(s)^000000.";
	else if( .@Time_Left > 60 )
		mes "Tiempo Restante : ^0000FF" + (.@Time_Left / 60) + " minuto(s)^000000.";
	else
		mes "Tiempo Restante : ^0000FF" + (.@Time_Left) + " segundo(s)^000000.";
	next;

	mes "[^2d8bd2Premio-Misión^000000]";
	mes "Base Exp : ^0000FF" + .@Mission_Exp + "^000000";
	mes "Job Exp : ^0000FF" + .@Mission_Job + "^000000";
	mes "Zeny : ^0000FF" + .@Mission_Zeny + "^000000";
	mes "Puntos : ^0000FF" + .@Mission_Points + "^000000";
	return;
}

prt_in,94,73,0	script	Contratos	111,{
	if( Mission_Tick <= 0 || Mission_ID1 == 0) 
		end;
	callfunc "MissionInfo";
	close;
}

prt_in,90,72,4	script	Issan::HuntingSystem	732,{
	mes "[^004000Issan^000000]";
	if ( BaseLevel < 50 )
	{
		mes "Vuelve cuando seas al menos nivel 50!";
		close;
	}
	if( Mission_Tick < gettimetick(2) )
	{
		if( Mission_Tick == 0 )
		{
			mes "Hola...";
			mes "Hola ese Issac ya estaba un poco viejo, sus misiones eran horribles, yo soy su hermano gemelo Issan, buscas algun trabajo???";
			next;
			mes "[^004000Issan^000000]";
			mes "Para explicarte, busco personas interesadas en realizar ^0000FFMisiones de Cacería^000000 cada día.";
			mes "Las misiones constan de eliminar criaturas que te asignaré.";
			next;
			mes "[^004000Issan^000000]";
			mes "Estas criaturas serán para todo el servidor.";
			mes "Pero no te preocupes, puedes realizar el trabajo en grupos de amigos, y cualquiera del equipo puede ser quien elimine y a ti se te cuenta.";
			next;
			mes "[^004000Issan^000000]";
			mes "Tendrás exactamente 24 horas para terminar una misión, si la terminas antes del tiempo debes venir a mi para pagarte el trabajo.";
			
		}
		else if( Mission_Ended )
		{
			mes "Tu nueva misión está lista, ya revisé los documentos.";
			mes "Estás listo para recibirla?";
		}
		else
		{
			mes "Según puedo ver fallaste tu última misión, no llegaste antes de las 24 horas o no eliminaste el total de criaturas solicitadas.";
			next;
			mes "[^004000Issan^000000]";
			mes "En fin, una lástima. Supongo que vienes por tu misión de hoy.";
			mes "Estás listo para recibirla?";
		}

		next;
		if( select("Si, cual es mi trabajo del día?:No gracias, por hoy no.") == 2 )
		{
			mes "[^004000Issan^000000]";
			mes "Suerte entonces, vuelve cuando cuentes con tiempo y recibirás buena paga.";
			close;
		}

		mes "[^004000Issan^000000]";
		mes "Un momento, déjame leer que te puedo asignar...";
		next;
		set Mission_Tick, gettimetick(2) + 86400;
		set Mission_Ended, 0;
		for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
			callfunc("mission_sethunting",.@i,getd("$Hunting"+.@i),getd("$HuntingQ"+.@i));
		callfunc "MissionInfo";
		next;
		mes "[^004000Issan^000000]";
		mes "Suerte en tu misión, recuerda que tienes 24 horas para completarla.";
		mes "Presentate conmigo cuando termines.";
		close;
	}
	else
	{
		mes "Hola, de vuelta por aca?";
		mes "Veamos como vas con el trabajo que te encargué...";
		next;
		
		if( Mission_Count1 + Mission_Count2 + Mission_Count3 + Mission_Count4 + Mission_Count5 < 1 )
		{
			mes "[^004000Issan^000000]";
			if( Mission_Ended == 0 )
			{
				mes "Excelente trabajo!! Terminaste tu misión del día sin problemas y antes del tiempo límite.";
				mes "Dejame pagarte por tus servicios.";
				next;

				set .@Mission_Lvl, 0;
				set .@Mission_Exp, 0;
				set .@Mission_Job, 0;
				for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
				{
					set .@MobID, getd("Mission_ID" + .@i);
					set .@Mission_Lvl, .@Mission_Lvl + strmobinfo(3, .@MobID);
					set .@Mission_Exp, .@Mission_Exp + (strmobinfo(6, .@MobID) * 8);
					set .@Mission_Job, .@Mission_Job + (strmobinfo(7, .@MobID) * 8);
				}
				set .@Mission_Zeny, .@Mission_Lvl * 500;
				if (.@Mission_Zeny > 250000)
					.@Mission_Zeny = 250000;
				
				
				set .@Mission_Lvl, .@Mission_Lvl / 4;
				
				if( .@Mission_Lvl < 50 )
					set .@Mission_Points, 1;
				else if( .@Mission_Lvl < 90 )
					set .@Mission_Points, 2;
				else
					set .@Mission_Points, 4;
				if( BaseLevel < .@Mission_Lvl - 20 )
				{
					mes "[^004000Issan^000000]";
					mes "Lo siento, pero eres demasiado bajo para recibir esta recompensa.";
					next;
					mes "[^004000Issan^000000]";
					mes "Debes ser al menos nivel ^ff0000"+ (.@Mission_Lvl - 20) +"^000000 para recibir la recompensa.";
					close;
				}
				
				// Mission Rewards...
				// =====================================================================
				getexp2 .@Mission_Exp, .@Mission_Job;
				set Zeny, Zeny + .@Mission_Zeny;
				set Mission_Points, Mission_Points + .@Mission_Points;
				if( Mission_Rank < 449 ) set Mission_Rank, Mission_Rank + 1;
				set Mission_Completed, Mission_Completed + 1;
				if( Mission_Completed > $TMission_Score )
				{
					npctalk "Felicitaciones, eres el nuevo Top Hunter!!";
					set $TMission_Score, Mission_Completed;
					set $TMission_CharID, getcharid(0);
					set $TMission_Name$, strcharinfo(0);
					set $TMission_Job$, jobname(Class);
				}

				getitem 44000,1;
				// =====================================================================
				
				// Clear Mission Data
				for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
					callfunc("mission_sethunting",.@i,0,0);
				set Mission_Ended, 1;
			}
			
			mes "Tu misión diaria ya esta terminada.";

			set .@Time_Left, Mission_Tick - gettimetick(2);
			if( .@Time_Left > 3600 )
				mes "Te asignaré una nueva misión en ^0000FF" + (.@Time_Left / 3600) + " horas^000000.";
			else if( .@Time_Left > 60 )
				mes "Te asignaré una nueva misión en ^0000FF" + (.@Time_Left / 60) + " minutos^000000.";
			else if( .@Time_Left > 0 )
				mes "Te asignaré una nueva misión en ^0000FF" + (.@Time_Left) + " segundos^000000.";
			else
				mes "Dejame ordenar mis documentos, vuelve a hablarme para asignarte tu nueva misión.";
			close;
		}

		mes "[^004000Issan^000000]";
		mes "Aún no completas la misión, te faltan criaturas por eliminar.";
		mes "Consulta el cartél a mi izquierda para ver el estado de tu trabajo.";
		close;
	}
	
OnPCLoadMapEvent:
	showevent QTYPE_QUEST,0;
	end;

OnNPCKillEvent:
	set .@partyid,getcharid(1);
	if ( .@partyid ) {
		set .@aid,getcharid(3);
		set .@map$,strcharinfo(3);
		set .@killedrid, killedrid;
		getpartymember .@partyid,1;
		getpartymember .@partyid,2;
		while( .@i < $@partymembercount ) {
			if ( isloggedin( $@partymemberaid[.@i],$@partymembercid[.@i] ) )
				if ( attachrid( $@partymemberaid[.@i] ) )
				{
					set .@Time_2Left, Mission_Tick - gettimetick(2);
					if ( strcharinfo(3) == .@map$ && Hp >= 1 ) {
						for( set .@h, 1; .@h < 6; set .@h, .@h + 1 )
						{
							set .@MobID, getd("Mission_ID" + .@h);
							set .@amount, getd("Mission_Count" + .@h);
							if(.@killedrid == .@MobID && .@Time_2Left > 0) {
								if( .@amount != 0 ) 
								{
									set .@amount, .@amount-1;
									setd "Mission_Count"+.@h, .@amount;
									if(.@amount > 0)
										message strcharinfo(0), "Te faltan: "+ .@amount + " x "+strmobinfo(2,.@MobID) + " (ID : " + .@MobID + ")";
									else
										message strcharinfo(0), "Terminaste de cazar: "+strmobinfo(2,.@MobID) + " (ID : " + .@MobID + ")";
								}
							}
						}
					}
				}
			set .@i,.@i + 1;
		}
		attachrid( .@aid );
	}
	else {
		set .@Time_Left, Mission_Tick - gettimetick(2);
		for( set .@h, 1; .@h < 6; set .@h, .@h + 1 )
		{
			set .@MobID, getd("Mission_ID" + .@h);
			set .@amount, getd("Mission_Count" + .@h);
			if(killedrid == .@MobID && .@Time_Left > 0) {
				if( .@amount != 0 ) 
				{
					set .@amount, .@amount-1;
					setd "Mission_Count"+.@h, .@amount;
					if(.@amount > 0)
						message strcharinfo(0), "Te faltan: "+ .@amount + " x "+strmobinfo(2,.@MobID) + " (ID : " + .@MobID + ")";
					else
						message strcharinfo(0), "Terminaste de cazar: "+strmobinfo(2,.@MobID) + " (ID : " + .@MobID + ")";
				}
			}
		}
	}
	end;
}

function	script	caceria	{
	set .@mission_tick, getarg(0); // Mission_Tick
	set .@partyid,getarg(1); // getcharid(1)
	set .@aid,getarg(2); // getcharid(3)
	set .@map$,getarg(3); // strcharinfo(3)
	set .@killedrid,getarg(4); // killedrid
	if ( .@partyid ) {
		getpartymember .@partyid,1;
		getpartymember .@partyid,2;
		while( .@i < $@partymembercount ) {
			if ( isloggedin( $@partymemberaid[.@i],$@partymembercid[.@i] ) )
				if ( attachrid( $@partymemberaid[.@i] ) )
				{
					set .@Time_2Left, Mission_Tick - gettimetick(2);
					if ( strcharinfo(3) == .@map$ && Hp >= 1 ) {
						for( set .@h, 1; .@h < 6; set .@h, .@h + 1 )
						{
							set .@MobID, getd("Mission_ID" + .@h);
							set .@amount, getd("Mission_Count" + .@h);
							if(.@killedrid == .@MobID && .@Time_2Left > 0) {
								if( .@amount != 0 ) 
								{
									set .@amount, .@amount-1;
									setd "Mission_Count"+.@h, .@amount;
									if(.@amount > 0)
										message strcharinfo(0), "Te faltan: "+ .@amount + " x "+strmobinfo(2,.@MobID) + " (ID : " + .@MobID + ")";
									else
										message strcharinfo(0), "Terminaste de cazar: "+strmobinfo(2,.@MobID) + " (ID : " + .@MobID + ")";
								}
							}
						}
					}
				}
			set .@i,.@i + 1;
		}
		attachrid( .@aid );
	}
	else {
		set .@Time_2Left, .@mission_tick - gettimetick(2);
		for( set .@h, 1; .@h < 6; set .@h, .@h + 1 )
		{
			set .@MobID, getd("Mission_ID" + .@h);
			set .@amount, getd("Mission_Count" + .@h);
			if(.@killedrid == .@MobID && .@Time_2Left > 0) {
				if( .@amount != 0 ) 
				{
					set .@amount, .@amount-1;
					setd "Mission_Count"+.@h, .@amount;
					if(.@amount > 0)
						message strcharinfo(0), "Te faltan: "+ .@amount + " x "+strmobinfo(2,.@MobID) + " (ID : " + .@MobID + ")";
					else
						message strcharinfo(0), "Terminaste de cazar: "+strmobinfo(2,.@MobID) + " (ID : " + .@MobID + ")";
				}
			}
		}
	}
	end;
}
prt_in,86,72,4	script	Top Hunter	857,{
	//set Mission_Tick, gettimetick(2);
	mes "[^2d8bd2Mejor Cazador^000000]";
	if( $TMission_CharID )
	{
		mes "Nombre : ^0000FF" + $TMission_Name$ + "^000000";
		mes "Profesión : ^0000FF" + $TMission_Job$ + "^000000";
		mes "Misiones : ^0000FF" + $TMission_Score + "^000000";
	}
	else
	{
		mes "Nombre : ^0000FF(ninguno)^000000";
		mes "Profesión : ^0000FF(ninguno)^000000";
		mes "Misiones : ^0000FF0^000000";
	}

	next;
	mes "[^2d8bd2Mejor Cazador^000000]";
	mes "El puesto al mejor cazador es asignado a quien tenga primero el mayor número de misiones terminadas.";
	close;
}

prt_in,92,73,4	script	Mision del dia	111,{
	mes "[^2d8bd2Tablero del día^000000]";
	set .@Mission_Lvl, 0;
	set .@Mission_Exp, 0;
	set .@Mission_Job, 0;
	for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
	{
		set .@MobID, getd("$Hunting" + .@i);
		set .@Mission_Lvl, .@Mission_Lvl + strmobinfo(3, .@MobID);
		set .@Mission_Exp, .@Mission_Exp + (strmobinfo(6, .@MobID) * (9-.@i));
		set .@Mission_Job, .@Mission_Job + (strmobinfo(7, .@MobID) * (9-.@i));
	}
	
	set .@Mission_Zeny, .@Mission_Lvl * 500;
	if (.@Mission_Zeny > 250000)
		.@Mission_Zeny = 250000;

	set .@Mission_Lvl, .@Mission_Lvl / 4;
	
	if( .@Mission_Lvl < 50 )
		set .@Mission_Points, 1;
	else if( .@Mission_Lvl < 90 )
		set .@Mission_Points, 2;
	else
		set .@Mission_Points, 4;
	
	mes "Nivel : ^0000FF" + .@Mission_Lvl + "^000000";

	if( .@Mission_Lvl < 50 )
	{
		mes "Dificultad : ^008000Bajo^000000";
		set .@Mission_Points, 1;
	}
	else if( .@Mission_Lvl < 90 )
	{
		mes "Dificultad : ^000080Media^000000";
		set .@Mission_Points, 2;
	}
	else
	{
		mes "Dificultad : ^FF0000Alta^000000";
		set .@Mission_Points, 4;
	}
	next;
	mes "[^2d8bd2Tablero del día^000000]";
	for( set .@i, 1; .@i < 6; set .@i, .@i + 1 )
	{
		set .@MobID, getd("$Hunting" + .@i);
		mes "^0000FF" + getd("$HuntingQ" + .@i) + "^000000 x ^2d8bd2" + strmobinfo(2,.@MobID) + "^000000 (ID : " + .@MobID + ")";
	}
	next;
	mes "[^2d8bd2Premio-Misión^000000]";
	mes "Base Exp : ^0000FF" + .@Mission_Exp + "^000000";
	mes "Job Exp : ^0000FF" + .@Mission_Job + "^000000";
	mes "Zeny : ^0000FF" + .@Mission_Zeny + "^000000";
	mes "Puntos : ^0000FF" + .@Mission_Points + "^000000";
	
	close;
}

-	pointshop	NanasShop	112,Mission_Points,41040:5,41041:5,41042:5,41043:5,41044:5,41045:5,40001:10,40002:10
prt_in,0,0,1	itemshop	NanasShop2	111,44000,41030:3,41000:5,41010:5,41011:5,41012:5,41013:5,41014:5,41015:5

prt_in,0,0,1	itemshop	NanasShop3	111,44000,505:1

prt_in,85,61,6	script	Yosi::HuntingRewards	725,{
	set .@name$,"[^400040Yosi^000000]";
	mes .@name$;
	mes "Buenas, soy Yosi la esposa de Issan.";
	mes "Cambiaré tus puntos de misión y medallas por artículos.";
	next;
	mes .@name$;
	mes "Tus estadísticas:";
	mes "- Misiones Completas: ^0000FF" + Mission_Completed + "^000000";
	mes "- Puntos de Misiones: ^0000FF" + Mission_Points + "^000000";
	mes "- Medallas de Misión: ^0000FF" + countitem(44000) + "^000000";
	mes "¿Qué quieres hacer?";
	next;
	switch(select("[ > ] Invertir Puntos:[ > ] Cambiar Medallas:[ > ] Cambiar aura")){
		case 1:
			mes .@name$;
			mes "Estos artículos solo se pueden conseguir con Puntos de Misiones.";
			close2;
			callshop "NanasShop";
			end;
		case 2:
			mes .@name$;
			mes "Estos artículos solo se pueden conseguir con Medallas de Misiones.";
			close2;
			callshop "NanasShop2";
			end;
		case 3:
			setarray .@auras_max[1], 152, 117, 35;
			mes .@name$;
			mes "que parte del aura deseas desbloquear o cambiar?";
			next;
			set .@s, select(
			( aura1 )?"^10B2D2 [ > ] ^000000 Aura 1":"^ff1919 [ > ] ^000000 Aura 1 ( 5 ^ff0000"+getitemname(44000)+"^000000)",
			( aura2 )?"^10B2D2 [ > ] ^000000 Aura 2":"^ff1919 [ > ] ^000000 Aura 2 ( 5 ^ff0000"+getitemname(44000)+"^000000)",
			( aura3 )?"^10B2D2 [ > ] ^000000 Aura 3":"^ff1919 [ > ] ^000000 Aura 3 ( 5 ^ff0000"+getitemname(44000)+"^000000)"
			);
			set .@revert, get_aura_part(.@s);
			set .@current, 0;
			switch( .@s  )
			{
				case 1:
					if( aura1 == 0)
					{
						if(countitem(44000) < 5 && countitem(41046) < 1)
						{
							mes .@name$;
							mes "Desbloquear esta parte cuesta 5 ^ff0000"+getitemname(44000)+"^000000.";
							close;
						}
						if(countitem(41046) < 1)
							delitem 44000,5;
						else
							delitem 41046,1;
						set aura1,1;
						
					}
					break;
				case 2:
					if( aura2 == 0)
					{
						if(countitem(44000) < 5 && countitem(41047) < 1)
						{
							mes .@name$;
							mes "Desbloquear esta parte cuesta 5 ^ff0000"+getitemname(44000)+"^000000.";
							close;
						}
						if(countitem(41047) < 1)
							delitem 44000,5;
						else
							delitem 41047,1;
						set aura2,1;
					}
					break;
				case 3:
					if( aura3 == 0)
					{
						if(countitem(44000) < 5 && countitem(41048) < 1)
						{
							mes .@name$;
							mes "Desbloquear esta parte cuesta 5 ^ff0000"+getitemname(44000)+"^000000.";
							close;
						}
						if(countitem(41048) < 1)
							delitem 44000,5;
						else
							delitem 41048,1;
						set aura3,1;
					}
					break;
			}
			mes .@name$;
			mes "Selecciona el aura que te guste y pulsa ^0000ffCancel^000000 o ^0000ffElegir esta aura^000000.";
			next;
			while(1)
			{
				set_aura_part(.@s, .@current);
				message strcharinfo(0),"Aura "+ .@s +" part == " + .@current;
				set .@menu$, "Siguiente (^0055FF"+((.@current!=.@auras_max[.@s])?.@current+1:1)+"^000000):Anterior (^0055FF"+((.@current!=1 && .@current!=0)?.@current-1:.@auras_max[.@s])+"^000000):Ir a...:Elegir esta aura:Elegir la que tenía";
				switch (select(.@menu$)) 
				{
					case 1: 
						set .@current, ((.@current!=.@auras_max[.@s])?.@current+1:1);
						break;
					case 2:
						set .@current, ((.@current!=1 && .@current!=0)?.@current-1:.@auras_max[.@s]); 
						break;
					case 3: 
						message strcharinfo(0),"Elige un número entre 1 - "+.@auras_max[.@s]+".";
						input .@current, 0, .@auras_max[.@s];
						break;
					case 4:
						close2;
						end;
					case 5: 
						set .@current, .@revert; set_aura_part(.@s, .@revert);
						close2;
						end;
				}
			}
			end;
			close;
	}
}


function	script	mission_sethunting	{
	.@index = getarg(0);
	.@id    = getarg(1);
	.@amount  = getarg(2);
	
	setd "Mission_ID"+.@index, .@id;
	setd "Mission_Count"+.@index, .@amount;
	return;
}