//===== rAthena Script =======================================
//= Wave Mode
//===== Description: =========================================
//= [Walkthrough Conversion]
//= Wave Mode Forest and Sky Instances
//- Officially monsters can use their skills
//    (NPC_SUMMONSLAVE, at least). NPC_EMOTION and
//    NPC_EMOTION_ON have been blocked since it disrupts the
//    walk system of the instance on rAthena.
//- Note: The instance is currently disabled on KRO.
//===== Changelogs: ==========================================
//= 1.0 First version. [Capuche]
//============================================================

// Simple function to move the monster when idle.
// Move the monster to the next coordinates + end the script on successful; kill the monster + return on failure
// callfunc( "F_mobidle", <npc name + idle label>, <size coord>, <defined x array>, <x shift>,  <defined y array>, <defined spot-spot distance array> );
function	script	F_mobidle	{
	sleep2 1000;	// stop if no RID

	.@game_id = getattachedrid();
	.@dist_min = 300;
	.@size = getarg(1);
	.@dx = getarg(3);
	getunitdata .@game_id, .@data;

	for ( .@i = 0; .@i < .@size; .@i++ ) {
		.@dist_to_spot[.@i] = distance( .@data[UMOB_X], .@data[UMOB_Y], (getelementofarray( getarg(2),.@i ) + .@dx), getelementofarray( getarg(4),.@i ) );
		if (.@dist_min >= .@dist_to_spot[.@i]) {
			.@dist_min = .@dist_to_spot[.@i];
			.@index = .@i;
		}
	}

	if (.@dist_min <= 1) {
		.@index++;
		if (.@index >= .@size) {
			unitskilluseid .@game_id,301,1,.@game_id,1,false,41;	// SA_INSTANTDEATH
			return;
		}
	}
	else if (.@index < (.@size -1)) {
		.@total[0] = .@dist_min + getelementofarray( getarg(5), .@index );
		.@total[1] = .@dist_to_spot[.@index + 1] + getelementofarray( getarg(5), .@index+1 );
		if (.@total[0] > .@total[1])
			.@index = .@index + 1;
	}

	if (.@dist_to_spot[.@index] > 14) {
		unitkill .@game_id;
		end;
	}
	if (!.@data[UMOB_TARGETID]) {
		unitwalk .@game_id, (getelementofarray( getarg(2),.@index ) + .@dx), getelementofarray( getarg(4),.@index );
		sleep2 50;	// for now a delay between unitwalk and mob_setidleevent is needed
	}
	mob_setidleevent .@game_id, getarg(0);
	end;
}

instance,43,66,5	script	Old Sign#8	4_BOARD3,{
	for (set .@i, 1; .@i <= 4; set .@i, .@i + 1) {
		mes "-- Fallen Angel Records --";
		.@FallenAngel_name$ = getd("$8_str_FallenAngel"+.@i+"$");
		.@FallenAngel_round = getd("$8_round_FallenAngel"+.@i);
		.@FallenAngel_timer  = getd("$8_num_FallenAngel"+.@i);
		.@FallenAngel_member = getd("$8_mem_FallenAngel"+.@i);
		
		.@time_m = .@FallenAngel_timer / 60;
		.@time_s = .@FallenAngel_timer - (.@time_m * 60);
		if (.@FallenAngel_name$ == "") {
			mes "Aún no hay registros de esta instancia.";
		}
		else {
			if (.@FallenAngel_timer < 60)
				.@time_txt$ = callfunc( "F_InsertPlural", .@time_s, "Segundo" );
			else
				.@time_txt$ = callfunc( "F_InsertPlural", .@time_m, "Minuto" ) + " " + callfunc( "F_InsertPlural", .@time_s, "Segundo" );
			if (.@FallenAngel_member > 0)
				.@string$ =  " con ^ff0044" + .@FallenAngel_member + " miembros^000000";
			mes "Mayor cantidad de rondas para ^ff0044" + .@FallenAngel_name$ + "^000000" + .@string$ + ".";
			mes "Hicieron ^ff0044" + .@FallenAngel_round + "^000000 rondas.";
			mes "y tardaron ^0000ff" + .@time_txt$ + "^000000.";
		}
		mes "Dificultad " + .dificultad$[.@i];
		if( .@i == 4 )
			close;
		else
			next;
	}
	close;

OnInit:
	for (set .@i, 1; .@i <= 4; set .@i, .@i + 1) {
		if (!getd("$8_num_FallenAngel"+.@i))
			setd "$8_num_FallenAngel"+.@i,3600;
	}
	setarray .dificultad$,"","^008000Easy^000000","Normal","^BA7100Hard^000000","^ff0000Insane^000000";
	end;
}

instance,41,70,1	script	Belka	4_F_ZONDAGIRL,{
	.@player_name$ = strcharinfo(0);
	getmapxy .@map_name$,.@x,.@y, BL_PC;

	if (is_party_leader() == true) {
		.@party_id = getcharid(1);
		getpartymember .@party_id, 1;
		getpartymember .@party_id, 2;
		for ( .@i = 0; .@i < $@partymembercount; .@i++ ) {
			if (isloggedin($@partymemberaid[.@i], $@partymembercid[.@i]) == 1)
				.@count_online++;
		}
		.@menu_entry$ = "Donde firmo?";
	}

	mes "[Belka]";
	mes "Bienvenido aventurero, estamos reclutando para salvar la inocencia de cada persona, deseas sumarte?";
	next;
	switch( select( "¿En qué consiste?", .@menu_entry$, "Ir a ^6B9900<Fallen Angel>^000000.", "Cancelar." ) ) {
	case 1:
		mes "[Belka]";
		mes "Ofrecemos un servicio donde podrás viajar a las alturas. Y podrás ayudar a defender a los ayudantes de santa.";
		next;
		mes "[Belka]";
		mes "En todo caso, pondrás tu vida en riesgo como en muchas otras partes, pero esta organización es sin fines de lucro.";
		next;
		mes "[Belka]";
		mes "Todos los costos son cubiertos por nuestra empresa, salvo el viaje a la parte superior, donde cada aventurero debe costearlo...";
		next;
		mes "[Belka]";
		mes "Por el día de hoy ofrecemos sólamente llevarte a nada más y nada menos que al mismisimo cielo!";
		next;
		mes "[Belka]";
		mes "¿Tienes alguna pregunta sobre nuestro proyecto?";
		while(true) {
			next;
			switch( select( "Hablamos de esos ^6B9900<Ángeles>^000000", "¿Existe alguna ^6B9900<recompensa>^000000?", "No." ) ) {
			case 1:
				mes "[Belka]";
				mes "Los ^6B9900<Ángeles>^000000";
				mes "son los mismísmos ayudantes de santa, y buscan crear un equilibrio cada año, pero existen criaturas demoniacas que no desean cooperar.";
				next;
				mes "[Belka]";
				mes "Tu misión es cooperar y ayudar a estos pobres caídos del cielo..";
				break;
			case 2:
				mes "[Belka]";
				mes "Bueno ^6B9900<recompensas>^000000 mejor dicho,";
				mes "todos amamos a Santa Claus, y si ayudamos a sus pobres angeles, es muy probable que nos ayude a nosotros también.";
				next;
				mes "[Belka]";
				mes "Pero mientras más gente, menos recompensas.";
				break;
			case 3:
				mes "[Belka]";
				mes "Muchas gracias por visitarnos, recuerda firmar por nuestra noble causa.";
				close;
			}
			next;
			mes "[Belka]";
			mes "¿Alguna otra pregunta?";
		}
	case 2:
		mes "[Belka]";
		mes "Muy amable, veamos el papeleo";
		next;
		setarray .@instance_name$[0], "Wave Mode";
		mes "[Belka]";
		mes "" + .@count_online + " party " + (.@count_online == 1 ? "miembro" : "miembros") + " encontrados " + .@player_name$ + ", cierto? Para ingresar a ayudar a estos ^6B9900<Ángeles caídos>^000000, por favor firma aquí.";
		next;
		switch(select("^3CB371[»]^000000 ^008000Modo Easy^000000","^3CB371[»]^000000 Modo Normal","^3CB371[»]^000000 ^BA7100Modo Hard^000000","^3CB371[»]^000000 ^ff0000Modo Insane^000000")){
			case 1:
				set .@level$,ID_EASY;
				break;
			case 2:
				set .@level$,ID_NORMAL;
				break;
			case 3:
				set .@level$,ID_HARD;
				break;
			case 4:
				set .@level$,ID_INSANE;
				break;
		}
		select("Firmar.");
		mes "[Belka]";
		mes "" + .@player_name$ + " has solicitado que te llevemos, por favor vuelve a hablarme cuando estes listo para viajar a ver a estos ingresar ^6B9900<Ángeles caídos>^000000.";
		if (instance_create(.@instance_name$[.@s],IM_PARTY,.@party_id,.@level$) < 0) {
			mes "Party: " + getpartyname( getcharid(1) );
			mes "Party Lider: " + strcharinfo(0);
			mes "^0000ff" + .@instance_name$[.@s] + " ^000000 - Ha fallado.";
			close;
		}
		set .@instanceid,instance_id(IM_PARTY);
		if (instance_getdifficulty(.@instanceid) == 4){
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_ATK,20);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_MAXHP,20);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_SPEED,20);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_MATK,20);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_CAST,20);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_ASPD,20);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_MAXSP,20);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_ALLSTATS,20);
		}
		if (instance_getdifficulty(.@instanceid) == 3){
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_ATK,10);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_MAXHP,10);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_SPEED,10);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_MATK,10);
			instance_setpenalty(instance_id(IM_PARTY),SC_ID_CAST,10);
		}
		if (instance_getdifficulty(.@instanceid) == 1){
			instance_setbuff(instance_id(IM_PARTY),SC_II_MAXHP,5);
			instance_setbuff(instance_id(IM_PARTY),SC_II_MAXSP,5);
			instance_setbuff(instance_id(IM_PARTY),SC_II_ALLSTATS,5);
		}
		close;
	case 3:
		switch( instance_enter("Wave Mode") ) {
		case IE_OTHER:
			mes "[Belka]";
			mes "Un error desconocido ha ocurrido.";
			close;
		case IE_NOINSTANCE:
		case IE_NOMEMBER:
			mes "[Belka]";
			mes "Actualmente no existen registros para tu viaje, por favor recuerda firmar.";
			close;
		case IE_OK:
			mapannounce .@map_name$, "" + .@player_name$ + " de la party " + getpartyname(getcharid(1)) + " ha ingresado a <Fallen Angel>.", bc_map,0xFF99;
			// warp "1@def02",29,35;
			break;
		}
		break;
	case 5:
		mes "[Belka]";
		mes "Muchas gracias por escucharme, recuerda que estas invitado a firmar.";
		close;
	}
	wave_mode_map$ = .@map_name$;
	wave_mode_x = .@x;
	wave_mode_y = .@y;
	end;
}

1@def02,3,1,5	script	#globaltimer8	CLEAR_NPC,{
	end;
OnT_s:
	initnpctimer;
	end;
OnT_s2:
	.@Fallen_timer = getd("$8_num_FallenAngel"+'8_num_FallenDif);
	if (!.@Fallen_timer)
		.@Fallen_timer = 14400;
	.@Fallen_Round = getd("$8_round_FallenAngel"+'8_num_FallenDif);
	getmapxy 'map_name$, .@x, .@y, BL_NPC;
	.@FallenAngel_member = getmapusers('map_name$);
	.@winner_name$ = '8_str_FallenParty$;
	.@current_timer = getnpctimer(0) / 1000;
	if (.@current_timer < 1)
		end;
	.@time_m = .@current_timer / 60;
	.@time_s = .@current_timer % 60;
	if (.@current_timer < 60)
		.@time_txt$ = callfunc( "F_InsertPlural", .@time_s, "Second" );
	else
		.@time_txt$ = callfunc( "F_InsertPlural", .@time_m, "Minute" ) + " " + callfunc( "F_InsertPlural", .@time_s, "Second" );
		
	if( 'wave_num < .@Fallen_Round )
		mapannounce 'map_name$, "El número de rondas [" + 'wave_num + "] ha sido guardado. Pero no ha roto la marca.",bc_map,"0xffff33";
	else
	{
		if('wave_num == .@Fallen_Round)
		{
			if (.@current_timer >= .@Fallen_timer)
				mapannounce 'map_name$, "El tiempo de " + .@time_txt$ + " ha sido guardado. Pero no ha roto la marca.",bc_map,"0xffff33";
			else {
				announce "La party "+.@winner_name$ + " con " + .@FallenAngel_member + " miembro(s) ha(n) mejorado el tiempo en Fallen Angel (" + .@time_txt$ + ").",bc_all,"0xffff33",FW_NORMAL,15;
				setd "$8_str_FallenAngel"+'8_num_FallenDif+"$" , .@winner_name$;
				setd "$8_round_FallenAngel"+'8_num_FallenDif , 'wave_num;
				setd "$8_num_FallenAngel"+'8_num_FallenDif , .@current_timer;
				setd "$8_mem_FallenAngel"+'8_num_FallenDif , .@FallenAngel_member;
			}
		}
		else
		{
			announce "La party "+.@winner_name$ + " con " + .@FallenAngel_member + " miembro(s) ha(n) mejorado el número de rondas en Fallen Angel (" + 'wave_num + ").",bc_all,"0xffff33",FW_NORMAL,15;
			setd "$8_str_FallenAngel"+'8_num_FallenDif+"$" , .@winner_name$;
			setd "$8_round_FallenAngel"+'8_num_FallenDif , 'wave_num;
			setd "$8_num_FallenAngel"+'8_num_FallenDif , .@current_timer;
			setd "$8_mem_FallenAngel"+'8_num_FallenDif , .@FallenAngel_member;
		}	
	}
	end;

OnTimer3600000:
	stopnpctimer;
	end;
OnInstanceInit:
	hideonnpc instance_npcname("#globaltimer8");
	end;
}

// Wave mode
1@def02,30,35,0	script	#wave_mode_sky_entrance	WARPNPC,1,1,{
	end;
OnTouch:
	if ('status_instance == 1)
		end;
	else if ('status_instance == 0) {
		'status_instance = 1;
		initnpctimer;
		'8_str_FallenParty$ = strcharinfo(1);
		'8_num_FallenDif = instance_getdifficulty(instance_id(IM_PARTY));
		disablenpc instance_npcname("#wave_mode_sky_entrance");
	}
	else if ('status_instance == 2) {
		if (wave_mode_map$ == "")
			warp "instance",48,43;
		else {
			warp wave_mode_map$, wave_mode_x, wave_mode_y;
			wave_mode_map$ = "";
			wave_mode_x = wave_mode_y = 0;
		}
	}
	end;
OnTimer4000:
	stopnpctimer;
	donpcevent instance_npcname("#wave_mode_sky_system") + "::OnStart";
	end;
}

1@def02,1,1,0	script	#wave_mode_sky_system	-1,{
	end;
OnStart:
	initnpctimer;
	mapannounce 'map_def02$, "Belka: Ayuda! Se acerca una horda de demonios desde el norte!.", bc_map;
	donpcevent instance_npcname("#globaltimer8") + "::OnT_s";
	end;
OnTimer1000:
	//mapannounce 'map_def02$, "3", bc_map;
	end;
OnTimer2000:
	//mapannounce 'map_def02$, "2", bc_map;
	end;
OnTimer3000:
	//mapannounce 'map_def02$, "1", bc_map;
	end;
OnTimer4000:
	'wave_num++;
	if ('wave_num % 5) {
		donpcevent 'npc_name_mob$ + "::OnSpawn";
		donpcevent 'npc_name_mercenary$ + "::OnStart";
		donpcevent 'npc_name_mercenary$ + "::OnStart";
	}
	else {
		donpcevent 'npc_name_treasure$ + "::OnStart";
	}
	end;
OnTimer24000:
	mapannounce 'map_def02$, "Belka: Se observa una nueva horda de demonios! Mantenlos alejados de nuestros pobres ángeles!", bc_map;
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}

1@def02,1,1,0	script	#wave_mode_sky_treasure	-1,{
	end;
OnStart:
	if ('treasure_num < 5)
		'treasure_num++;
	mapannounce 'map_def02$, "Belka: !! " + 'treasure_num + " tesoro(s) ha(n) aparecido, Gracias Santa!!", bc_map;
	for ( .@i = 0; .@i < 'treasure_num; .@i++ ) {
		monster 'map_def02$,0,0, "Treasure box",3075,1;	// WA_TREASURE
		'treasure_gid[.@i] = $@mobid[0];
	}
	initnpctimer;
	end;
OnTimer16500: callsub( S_Dice, ET_DICE3 );
OnTimer17500: callsub( S_Dice, ET_DICE2 );
OnTimer18500: callsub( S_Dice, ET_DICE1 );
OnTimer19500:
	for ( .@i = 0; .@i < 'treasure_num; .@i++ ) {
		if (unitexists('treasure_gid[.@i]))
			unitskilluseid 'treasure_gid[.@i],301,1,'treasure_gid[.@i],-1;	// SA_INSTANTDEATH
	}
	donpcevent 'npc_name_treasure$ + "::OnStop";
	end;

S_Dice:
	.@emotion_num = getarg(0);
	for ( .@i = 0; .@i < 'treasure_num; .@i++ ) {
		if (unitexists('treasure_gid[.@i]))
			emotion .@emotion_num, 'treasure_gid[.@i];
	}
	end;

OnStop:
	deletearray 'treasure_gid[0], 'treasure_num;
	stopnpctimer;
	end;
}

1@def02,1,1,0	script	#wave_mode_sky_mercenary	-1,{
	end;
OnStart:
	monster 'map_def02$,29,35, "Mercenary", 3086,1, "",0,AI_WAVEMODE;	// WA_MERCENARY
	.@gid = $@mobid[0];
	emotion ET_SURPRISE, .@gid;
	setunitdata .@gid, UMOB_IGNORE_CELL_STACK_LIMIT, true;
	mob_setidleevent .@gid, 'npc_name_mercenary$ + "::OnIdle";
	sleep 45000;
	if (unitexists(.@gid))
		unitskilluseid .@gid,301,1,.@gid,1,false;	// SA_INSTANTDEATH
	end;

OnIdle:
	callfunc( "F_mobidle", ('npc_name_mercenary$ + "::OnIdle"), 'size_coord, 'x_merc, 0, 'y_merc, 'dist_spot_ZA );
	end;
}

1@def02,1,1,0	script	#wave_mode_sky_mob_spawn	-1,{
	end;
OnSpawn:
	mapannounce 'map_def02$, "-- Ronda " + 'wave_num + " --", bc_map;

	.@i = ('wave_num % 10) - 1;
	setarray .@mob_list[0],
		3076,	// WA_MONSTER_1
		3077,	// WA_MONSTER_2
		3078,	// WA_MONSTER_3
		3079,	// WA_MONSTER_4
		3080,	// WA_MONSTER_5 (skipped)
		3081,	// WA_MONSTER_6
		3082,	// WA_MONSTER_7
		3083,	// WA_MONSTER_8
		3084,	// WA_MONSTER_9
		3085;	// WA_MONSTER_10 (skipped)
	'mob_id = .@mob_list[.@i];
	initnpctimer;
	end;
OnTimer1000: callsub( S_Spawn, true );	// aggressive
OnTimer1500: callsub( S_Spawn, false );	// passive
OnTimer2000: callsub( S_Spawn, true );
OnTimer2500: callsub( S_Spawn, false );
OnTimer3000: callsub( S_Spawn, true );
OnTimer3500: callsub( S_Spawn, false );
OnTimer4000: callsub( S_Spawn, true );
OnTimer4500: callsub( S_Spawn, false );
OnTimer5000: callsub( S_Spawn, true );
OnTimer5500: callsub( S_Spawn, false );
OnTimer6000: callsub( S_Spawn, true );
OnTimer6500: callsub( S_Spawn, false );
OnTimer7000: callsub( S_Spawn, true );
OnTimer7500: callsub( S_Spawn, false );
OnTimer8000: callsub( S_Spawn, true );
OnTimer8500:
	stopnpctimer;
	end;

S_Spawn:
	monster 'map_def02$,48,67, "Demonio", 'mob_id,1;
	if (getarg(0) == true)
		setunitdata $@mobid[0], UMOB_MODE, ( MD_CANMOVE|MD_NORANDOMWALK|MD_AGGRESSIVE|MD_CANATTACK );
	.@gid = $@mobid[0];
	emotion ET_SURPRISE, .@gid;
	setunitdata .@gid, UMOB_IGNORE_CELL_STACK_LIMIT, true;
	mob_setidleevent .@gid, 'npc_name_mob$ + "::OnIdle";
	end;

OnIdle:
	// monster are walking to the nearest next spot
	callfunc( "F_mobidle", ('npc_name_mob$ + "::OnIdle"), 'size_coord, 'x_mob, 0, 'y_mob, 'dist_spot_AZ );
	'mob_escaped++;
	if ('mob_escaped <= 20)
		mapannounce 'map_def02$, "" + 'mob_escaped + " " + ('mob_escaped == 1 ? "demonio ha" : "demonios han") + " escapado.", bc_map;
	if ('mob_escaped == 20)
		donpcevent instance_npcname("#wave_mode_sky_out") + "::OnFail";
	end;
}


1@def02,1,1,0	script	#wave_mode_sky_out	-1,{
	end;
OnFail:
	// monsters timers continue
	donpcevent instance_npcname("#wave_mode_sky_system") + "::OnStop";

	mapannounce 'map_def02$, "Haz fallado! Los ángeles han caído.", bc_map;
	'status_instance = 2;
	enablenpc instance_npcname("#wave_mode_sky_entrance");
	initnpctimer;
	end;
OnTimer1000:
	donpcevent instance_npcname("#globaltimer8") + "::OnT_s2";
	mapannounce 'map_def02$, "<Fallen Angel> se ha cerrado. Debes volver donde perteneces, puedes usar el portal de la entrada.", bc_map;
	end;
OnTimer30000:
	stopnpctimer;
	instance_destroy();
	end;

OnInstanceInit:
	deletearray 'treasure_gid[0], 'treasure_num;
	'wave_num = 'treasure_num = 'status_instance = 'mob_escaped = 0;

	'map_def02$ = instance_mapname("1@def02");

	'npc_name_mob$ = instance_npcname("#wave_mode_sky_mob_spawn");
	'npc_name_treasure$ = instance_npcname("#wave_mode_sky_treasure");
	'npc_name_mercenary$ = instance_npcname("#wave_mode_sky_mercenary");

	disablenpc instance_npcname("#wave_mode_sky_out");

	setarray 'x_mob[0], 48, 38, 30, 30, 29, 38, 47, 51, 50, 42, 33, 29;
	setarray 'y_mob[0], 67, 67, 64, 58, 52, 52, 51, 45, 36, 36, 36, 35;

	setarray 'x_merc[0], 29, 33, 42, 50, 51, 47, 38, 29, 30, 30, 38, 47;
	setarray 'y_merc[0], 35, 36, 36, 36, 45, 51, 52, 52, 58, 64, 67, 67;

	'size_coord = getarraysize('x_mob);
	for ( .@i = 0; .@i < 'size_coord -1; .@i++ ) {
		.@dist_mob[.@i+1] = distance( 'x_mob[.@i], 'y_mob[.@i], 'x_mob[.@i+1], 'y_mob[.@i+1] );
		.@total_mob += .@dist_mob[.@i+1];
		.@dist_merc[.@i+1] = distance( 'x_merc[.@i], 'y_merc[.@i], 'x_merc[.@i+1], 'y_merc[.@i+1] );
		.@total_merc += .@dist_merc[.@i+1];
	}
	for ( .@i = 0; .@i < 'size_coord -1; .@i++ ) {
		'dist_spot_AZ[.@i] = .@total_mob - .@dist_mob[.@i];
		'dist_spot_ZA[.@i] = .@total_merc - .@dist_merc[.@i];
	}
	end;
}
