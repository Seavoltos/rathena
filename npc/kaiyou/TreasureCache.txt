//===== rAthena Script =======================================================
//= Treasure Cache
//===== By: ==================================================================
//= ~Seavoltos~
//===== Current Version: =====================================================
//= 2.0
//===== Compatible With: =====================================================
//=	rAthena
//===== Description: =========================================================
//= Treasure Cache
//===== Topic ================================================================
//= 
//===== Additional Comments: =================================================
//= 
//============================================================================

sec_in02,126,178,3	script	Treasure Cache	4_TREASURE_BOX,{
	mes "Una caja secreta donde nadie sabe lo que hay dentro.";
	mes "^4d4dffPuedes abrirla usando alguna de las siguientes llaves:\n";
	mes " - <ITEM>[Bronze Key]<INFO>41030</INFO></ITEM>\n";
	mes " - <ITEM>[Silver Key]<INFO>41032</INFO></ITEM>\n";
	mes " - <ITEM>[Gold Key]<INFO>41034</INFO></ITEM>.^000000";
	next;
	switch(select("^4d4dffAbrir usando 1 Bronze Key^000000","^4d4dffAbrir usando 1 Silver Key^000000","^4d4dffAbrir usando 1 Gold Key^000000")) {
		case 1:
			mes "^FF0000[Advertencia]^000000";
	        mes "^FF0000Intercambia un artículo aleatorio con una llave.^000000";
	        mes "^FF0000El artículo intercambiado no se puede devolver, ni se puede cambiar por otra llave.^000000";
	        next;
			if(select("Seguir adelante.","Dejar de hablar.") == 2) {
				mes "Decidiste no seguir con el proceso.";
				close;
			} else if (countitem("Bronze_Key_MvP") < 1) {
				mes "No tienes ninguna <ITEM>[Bronze Key]<INFO>41030</INFO></ITEM>.";
				close;
			} else {
				if (checkweight(1201,1) == 0 || ((MaxWeight - Weight) * 100 / MaxWeight) < 10) {
					mes "^4d4dffPor favor, primero revisa que tengas espacio suficiente en tu inventario.^000000";
					close;
				}
				delitem("Bronze_Key_MvP", 1);
				getgroupitem(IG_KACHUA_KEY);
				mes "El cofre se ha abierto!";
				mes "Tuviste suerte?";
				specialeffect2 EF_VALLENTINE;
				close;
			}
		case 2:
			mes "^FF0000[Advertencia]^000000";
	        mes "^FF0000Intercambia dos artículos aleatorios con una llave.^000000";
	        mes "^FF0000Los artículos intercambiados no se pueden devolver, ni se pueden cambiar por otra llave.^000000";
	        next;
			if(select("Seguir adelante.","Dejar de hablar.") == 2) {
				mes "Decidiste no seguir con el proceso.";
				close;
			} else if (countitem("Silver_Key_MvP") < 1) {
				mes "No tienes ninguna <ITEM>[Silver Key]<INFO>41032</INFO></ITEM>.";
				close;
			} else {
				delitem("Silver_Key_MvP", 1);
				getgroupitem(IG_KACHUA_KEY);
				getgroupitem(IG_KACHUA_KEY);
				mes "El cofre se ha abierto!";
				mes "Tuviste suerte?";
				specialeffect2 EF_VALLENTINE;
				close;
			}
			end;
		case 3:
			mes "^FF0000[Advertencia]^000000";
	        mes "^FF0000Intercambia tres artículos aleatorios con una llave.^000000";
	        mes "^FF0000Los artículos intercambiados no se pueden devolver, ni se pueden cambiar por otra llave.^000000";
	        next;
			if(select("Seguir adelante.","Dejar de hablar.") == 2) {
				mes "Decidiste no seguir con el proceso.";
				close;
			} else if (countitem("Gold_Key_MvP") < 1) {
				mes "No tienes ninguna <ITEM>[Gold Key]<INFO>41034</INFO></ITEM>.";
				close;
			} else {
				delitem("Gold_Key_MvP", 1);
				getgroupitem(IG_KACHUA_KEY);
				getgroupitem(IG_KACHUA_KEY);
				getgroupitem(IG_KACHUA_KEY);
				mes "El cofre se ha abierto!";
				mes "Tuviste suerte?";
				specialeffect2 EF_VALLENTINE;
				close;
			}
	}
}

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "qshop"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
batt_off,127,40,3	script	Quest#1	856,{ callfunc "qshop",1; }
// prontera,165,203,6	script	Quest Shop#2	998,{ callfunc "qshop",1,2; }	// call the shop 1 and 2 defined below
// etc.. Add your Shop NPCs 'Quest Shop#XXX' here
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	quest_shop	-1,{
function Add; function Chk; function Slot; function Cm;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,1;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,1;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"Treasure Cache",	// Shop Named 1
		"Oficiales",		// Shop Named 2
		"Otras";		// Shop Named 3

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

// Shop 1
	Add(1,5283,1,1000000,0,41061,1,1025,100,914,100,5015,1,916,100);
	Add(1,5208,1,1000000,0,41062,1,1001,40,1097,100,1006,10);
	Add(1,5527,1,1000000,0,41063,1,706,1,515,200,622,5,705,50,949,50);
	Add(1,5101,1,1000000,0,41064,1,983,2,1059,200,2208,1);
	Add(1,5181,1,1000000,0,41065,1,1038,200,1039,200,7209,20);
	Add(1,18539,1,1000000,0,41066,1,932,1000,1044,500,5017,1,7005,100);
	Add(1,18599,1,1000000,0,41067,1,932,500,1044,1000,5017,1,7005,100);
	Add(1,5654,1,1000000,0,41068,1,610,5,714,1,607,20,608,5);
	Add(1,5574,1,1000000,0,41069,1,1019,10,1068,100,998,20,752,5);
	Add(1,18580,1,1000000,0,41070,1,610,5,12002,8,607,20,608,5);
	Add(1,5377,1,1000000,0,41071,1,7319,20,1021,100,520,20,1752,2);
	Add(1,5531,1,1000000,0,41072,1,1036,100,7032,200,1768,20,1003,1);
	Add(1,5570,1,1000000,0,41073,1,969,5,731,50,732,250,717,500);
	Add(1,5255,1,1000000,0,41074,1,619,30,4001,3,938,300,2227,1);
	Add(1,5388,1,1000000,0,41075,1,954,200,937,2,678,2);
	Add(1,5498,1,1000000,0,41076,1,919,250,628,2,920,100,4029,1);
	Add(1,5465,1,1000000,0,41077,1,949,250,720,10,706,1,2513,1);
	Add(1,5401,1,1000000,0,41078,1,999,250,746,250,983,50,1001,50);
	Add(1,5597,1,1000000,0,41079,1,938,500,957,200,7126,300,587,25);
	Add(1,5362,1,1000000,0,41080,1,7157,25,7156,30,4230,1,13304,1);
	
	
	
// Shop 2
	Add(2,531,1,3,0,512,1,713,1);
	Add(2,532,1,3,0,513,1,713,1);
	Add(2,533,1,3,0,514,1,713,1);
	Add(2,534,1,3,0,515,1,713,1);

// Shop 3
	Add(3,531,1,3,0,512,1,713,1);
	Add(3,532,1,3,0,513,1,713,1);
	Add(3,533,1,3,0,514,1,713,1);
	Add(3,534,1,3,0,515,1,713,1);

// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "qshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"Un error ha ocurrido.";
		end;
	}
	dispbottom "Selecciona solo uno a la vez.";
	callshop "qshop"+@shop_index,1;
	npcshopattach "qshop"+@shop_index;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"No puedes comprar tantos "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Recompensa: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requisitos:\n";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+Cm((.@q[4]*.@q[1]))+" Zeny^000000\n";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000\n";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000\n";
	next;
	setarray @qe[1], getiteminfo(.@q[0], ITEMINFO_LOCATIONS), getiteminfo(.@q[0], ITEMINFO_VIEW);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Comprar ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Probar...":"")+": ~ ^777777Salir^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[Quest Shop]";
				mes "Lo siento, no cumples con todos los pre-requisitos.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000Necesitas "+(((.@q[2]*getiteminfo(.@q[0], ITEMINFO_WEIGHT))+Weight-MaxWeight)/10)+" de peso para poder completar el intercambio.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" ha creado "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Recompensa #"+getarg(1)+" invalida (saltada).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest prerequisito #"+getarg(.@i)+" invalida (saltada).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "qshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^1E8449";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0), ITEMINFO_TYPE) == 4 || getiteminfo(getarg(0), ITEMINFO_TYPE) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function Cm {
	set .@str$, getarg(0);
	for(set .@i,getstrlen(.@str$)-3; .@i>0; set .@i,.@i-3)
		set .@str$, insertchar(.@str$,",",.@i);
	return .@str$;
	}
}

function	script	qshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	qshop1	-1,909:-1
-	shop	qshop2	-1,909:-1
-	shop	qshop3	-1,909:-1
-	shop	qshop4	-1,909:-1
-	shop	qshop5	-1,909:-1
