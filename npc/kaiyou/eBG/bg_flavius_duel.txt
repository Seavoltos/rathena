
// This file is NOT public - you are not allowed to distribute it.

// ==============================================================================
// BattleGround System - TeamDeathMatch
// ==============================================================================

// Battleground Engine
// *********************************************************************

-	script	bg_duel	FAKE_NPC,{
	end;
	
OnTeam1Quit:
	bg_desert;
OnTeam1Die:
	getmapxy .@m$, .@x, .@y, BL_PC;
	if( .@y > 110) end;
	if( $@BG_Status_Duel == 1 )
	{
		set .Team1Count, .Team1Count - 1;
		donpcevent "bg_duel::OnValidateScore";
	}
	end;

OnTeam2Quit:
	bg_desert;
OnTeam2Die:
	getmapxy .@m$, .@x, .@y, BL_PC;
	if( .@y > 110) end;
	if( $@BG_Status_Duel == 1 )
	{
		set .Team2Count, .Team2Count - 1;
		donpcevent "bg_duel::OnValidateScore";
	}
	end;
	
OnGuillaumeActive:
	warp "bg_td",132,184;
	end;
	
OnCroixActive:
	warp "bg_td",151,184;
	end;

OnReady:
	if( $@BG_Status_Duel == 0 )
	{
		enablenpc "Therapist in battle#duel1";
		enablenpc "Therapist in battle#duel2";
		bgannounce "Meijing: El Battleground Duel ha iniciado!",$@BG_COLOR;
	}
	initnpctimer;
	set $@BG_Status_Duel,1;
	set .Team1Count, 99;
	set .Team2Count, 99;
	set .Team1Count, bg_get_data($@BG_DUEL_Team1, 0);
	set .Team2Count, bg_get_data($@BG_DUEL_Team2, 0);
	bg_updatescore "bg_td",.Team1_Score,.Team2_Score;
	sleep 2100;
	bg_warp $@BG_DUEL_Team1,"bg_td",132,184;
	bg_warp $@BG_DUEL_Team2,"bg_td",151,184;
	sleep 2100;
	//bg_team_reveal $@BG_DUEL_Team1;
	//bg_team_reveal $@BG_DUEL_Team2;
	donpcevent "#guiduel_respawn::OnBGStart";
	donpcevent "#croduel_respawn::OnBGStart";
	donpcevent "bg_duel::OnValidateScore";
	end;

OnValidateScore:
	if( $@BG_Status_Duel != 1 )
		end;

	bg_updatescore "bg_td",.Team1Count,.Team2Count;
	if(.Team1Count == 0)
	{//Team 2 won round
		set .Team2_Score,.Team2_Score+1;
		donpcevent "bg_duel::OnReady";
	} else if(.Team2Count == 0)
	{//Team 1 won round
		set .Team1_Score,.Team1_Score+1;
		donpcevent "bg_duel::OnReady";
	}
	set .@Team1Countp, bg_get_data($@BG_DUEL_Team1, 0);
	set .@Team2Countp, bg_get_data($@BG_DUEL_Team2, 0);
	if( .Team1_Score == 3 )
	{ // Team 1 Won
		mapannounce "bg_td","El ejercito de Blue Team ha ganado la batalla!",1,0x0000FF;
		set $@BG_DUEL_Victory,1;
	}
	else if( .Team2_Score == 3 )
	{ // Team 2 Won
		mapannounce "bg_td","El ejercito de Red Team ha ganado la batalla!",1,0xFF0000;
		set $@BG_DUEL_Victory,2;
	}
	else if( .@Team2Countp == 0 )
	{ // All Team 2 Players quit
		mapannounce "bg_td","El ejercito de Blue Team ha ganado la batalla!",1,0x0000FF;
		set $@BG_DUEL_Victory,3;
	}
	else if( .@Team1Countp == 0 )
	{ // All Team 1 Players quit
		mapannounce "bg_td","El ejercito de Red Team ha ganado la batalla!",1,0xFF0000;
		set $@BG_DUEL_Victory,3;
	}
	else end;
	donpcevent "bg_duel::OnMatchEnd";
	end;
OnTimer25000:
	set .Team1Count, bg_get_data($@BG_DUEL_Team1, 0);
	set .Team2Count, bg_get_data($@BG_DUEL_Team2, 0);
	bg_updatescore "bg_td",.Team1Count,.Team2Count;
	end;
OnTimer600000:
	mapannounce "bg_td","La batalla finalizará en 5 minutos!!",1,0x808000;
	end;

OnTimer840000:
	mapannounce "bg_td","La batalla finalizará en 1 minuto!!",1,0x808000;
	end;

OnTimer900000:
	if( .Team1_Score > .Team2_Score )
	{ // Team 1 Won
		set $@BG_DUEL_Victory,1;
		mapannounce "bg_td","El ejercito Blue Team ha ganado la batalla!",1,0x0000FF;
	}
	else if( .Team1_Score < .Team2_Score )
	{ // Team 2 Won
		set $@BG_DUEL_Victory,2;
		mapannounce "bg_td","El ejercito Red Team ha ganado la batalla!",1,0xFF0000;
	}
	else
	{
		set $@BG_DUEL_Victory,3;
		mapannounce "bg_td","La batalla ha sido un empate...!",1,0x808000;
	}
	bg_warp $@BG_DUEL_Team1,"bg_td",132,184;
	bg_warp $@BG_DUEL_Team2,"bg_td",151,184;
	sleep 1000;

OnMatchEnd:
	stopnpctimer;
	donpcevent "#guiduel_respawn::OnBGStop";
	donpcevent "#croduel_respawn::OnBGStop";
	
	disablenpc "Therapist in battle#duel1";
	disablenpc "Therapist in battle#duel2";
	
	set .Team1Countf, bg_get_data($@BG_DUEL_Team1, 0);
	set .Team2Countf, bg_get_data($@BG_DUEL_Team2, 0);
	
	
	
	sleep 1000;
	
	enablenpc "Vintenar#duel_a";
	enablenpc "Vintenar#duel_b";
	
	set $@BG_Status_Duel, 2;
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	
	mapannounce "bg_td","Meijing: Reclama tus recompensas, tienes 15 segundos!!",1,0x00FF00;
	sleep 15000;
	bg_reserve "bg_td", true;
	
	$@BG_Status_Duel = 0;
	if( $@BG_DUEL_Team1 ) { bg_destroy $@BG_DUEL_Team1; $@BG_DUEL_Team1 = 0; }
	if( $@BG_DUEL_Team2 ) { bg_destroy $@BG_DUEL_Team2; $@BG_DUEL_Team2 = 0; }
	bg_unbook "bg_td";
	
	disablenpc "Vintenar#duel_a";
	disablenpc "Vintenar#duel_b";
	mapwarp "bg_td","prontera",155,181;
	
	bg_updatescore "bg_td",0,0;
	end;

OnInit:
	mapwarp "bg_td","prontera",155,181;
	set .Team1_Score, 0;
	set .Team2_Score, 0;
	sleep 5000;
	bg_updatescore "bg_td",0,0;
	setwall "bg_td",138,178,12,0,0,"bg_duel1";
	setwall "bg_td",145,178,12,0,0,"bg_duel2";
	//setwall "rwc03",77,47,6,0,0,"bg_hunter_td3";
	//setwall "rwc03",85,47,6,0,0,"bg_hunter_td4";
	end;
}

// Battleground Rewards
// *********************************************************************
bg_td,128,184,5	script	Vintenar#duel_a	419,{
	if (getcharid(4) == $@BG_DUEL_Team1) {
		if ($@BG_DUEL_Victory == 1)
			callfunc "F_BG_Badge",1,"Guillaume","Duel",getvariableofnpc(.Team1Countf,"bg_duel");
		else
			callfunc "F_BG_Badge",0,"Guillaume","Duel",getvariableofnpc(.Team1Countf,"bg_duel");
	}
	else if(getcharid(4) == $@BG_DUEL_Team2) {
		if ($@BG_DUEL_Victory == 2)
			callfunc "F_BG_Badge",1,"Croix","Duel",getvariableofnpc(.Team2Countf,"bg_duel");
		else
			callfunc "F_BG_Badge",0,"Croix","Duel",getvariableofnpc(.Team2Countf,"bg_duel");
	}
	bg_leave;
	end;

OnInit:
	disablenpc "Vintenar#duel_a";
	end;
}

bg_td,155,184,3	script	Vintenar#duel_b	419,{
	if (getcharid(4) == $@BG_DUEL_Team1) {
		if ($@BG_DUEL_Victory == 1)
			callfunc "F_BG_Badge",1,"Guillaume","Duel",getvariableofnpc(.Team1Countf,"bg_duel");
		else
			callfunc "F_BG_Badge",0,"Guillaume","Duel",getvariableofnpc(.Team1Countf,"bg_duel");
	}
	else if(getcharid(4) == $@BG_DUEL_Team2) {
		if ($@BG_DUEL_Victory == 2)
			callfunc "F_BG_Badge",1,"Croix","Duel",getvariableofnpc(.Team2Countf,"bg_duel");
		else
			callfunc "F_BG_Badge",0,"Croix","Duel",getvariableofnpc(.Team2Countf,"bg_duel");
	}
	bg_leave;
	end;

OnInit:
	disablenpc "Vintenar#duel_b";
	end;
}

// Battleground Therapist
// *********************************************************************

bg_td,132,188,5	script	Therapist in battle#duel1	4_F_SISTER,10,10,{
	specialeffect2 EF_HEAL;
	sc_start 32, 600000, 10;  // Increase Agi
	sc_start 30, 600000, 10; // Blessing
	percentheal 100,100;
	repairall;
	sc_end SC_BERSERK;
	sc_end SC_STRIPWEAPON;
	sc_end SC_STRIPSHIELD;
	sc_end SC_STRIPARMOR;
	sc_end SC_STRIPHELM;

	end;
OnInit:
	disablenpc "Therapist in battle#duel1";
	end;
}

bg_td,151,188,3	script	Therapist in battle#duel2	4_F_SISTER,10,10,{
	specialeffect2 EF_HEAL;
	sc_start 32, 600000, 10;  // Increase Agi
	sc_start 30, 600000, 10; // Blessing
	percentheal 100,100;
	sc_end SC_BERSERK;
	sc_end SC_STRIPWEAPON;
	sc_end SC_STRIPSHIELD;
	sc_end SC_STRIPARMOR;
	sc_end SC_STRIPHELM;
	repairall;
	end;
OnInit:
	disablenpc "Therapist in battle#duel2";
	end;
}

// Battleground Respawn
// *********************************************************************

bg_td,132,188,0	script	#guiduel_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;
	
OnTimer400000:
OnTimer180000:
OnTimer25000:
	areapercentheal "bg_td",128,180,137,191,100,100;
	areawarp "bg_td",128,180,137,191,"bg_td",59,77,62,80;
	end;

}

bg_td,151,188,0	script	#croduel_respawn	HIDDEN_WARP_NPC,{
	end;

OnBGStart:
	initnpctimer;
	end;

OnBGStop:
	stopnpctimer;
	end;

OnTimer24000:
	specialeffect 83;
	end;
	
OnTimer400000:
OnTimer180000:
OnTimer25000:
	areapercentheal "bg_td",146,180,155,191,100,100;
	areawarp "bg_td",146,180,155,191,"bg_td",115,77,118,80;
	end;
}

// Flags
// *********************************************************************
bg_td,132,191,4	script	Flag#Duel_Blue	1_FLAG_LION,{
	end;
}
bg_td,151,191,4	script	Flag#Duel_Red	1_FLAG_EAGLE,{
	end;
}

// MapFlags
// *********************************************************************

bg_td	mapflag	battleground	2
bg_td	mapflag	nomemo
bg_td	mapflag	nosave	SavePoint
bg_td	mapflag	noteleport
bg_td	mapflag	nowarp
bg_td	mapflag	nowarpto
bg_td	mapflag	noreturn
bg_td	mapflag	nobranch
bg_td	mapflag	nopenalty
bg_td	mapflag	bg_consume
